/*
 * Copyright (c) 2014-2017 Afero, Inc. All rights reserved.
 */

apply plugin: 'com.android.library'

repositories {
    maven {
        url "https://afero.jfrog.io/afero/hubby-android"
        credentials {
            username = project.aferoArtifactoryUserName
            password = project.aferoArtifactoryPassword
        }
    }
}

final String hubbyVersion = "1.0.844"
final String hubType = project.findProperty('aferoSofthubType') ?: '"CONSUMER"'

android {
    compileSdkVersion 26

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 26

        versionCode 1
        versionName rootProject.version

        buildConfigField 'String', 'HUBBY_VERSION', '"'+hubbyVersion+'"'
        buildConfigField 'String', 'HUB_TYPE', hubType
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')

    implementation project(path: ':afero-sdk-core')

    api "io.afero.sdk:hubby:${hubbyVersion}@aar"

    implementation 'io.reactivex:rxjava:1.3.0'
    implementation 'com.android.support:support-annotations:28.0.0'

    // https://github.com/robolectric/robolectric
    testImplementation 'org.robolectric:robolectric:3.3.1'
    testImplementation 'junit:junit:4.12'
    testImplementation 'com.squareup.retrofit2:retrofit:2.3.0'
}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
    android.libraryVariants.all { variant ->
        if (variant.name == 'release') {
            classpath += variant.javaCompileProvider.get().classpath
        }
    }
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        aar(MavenPublication) {

            version = rootProject.version
            artifactId project.getName()
            groupId rootProject.group

            artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
            artifact javadocJar

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                configurations.api.allDependencies.each {
                    def hasGroup = it.group != null
                    def hasName = (it.name != null && !"unspecified".equals(it.name))
                    def hasVersion = (it.version != null && !"unspecified".equals(it.version))

                    if (hasGroup && hasName && hasVersion) {
                        def depNode = dependenciesNode.appendNode('dependency')
                        depNode.appendNode('groupId', it.group)
                        depNode.appendNode('artifactId', it.name)
                        depNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}


