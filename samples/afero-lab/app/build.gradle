/*
 * Copyright (c) 2014-2017 Afero, Inc. All rights reserved.
 */

apply plugin: 'com.android.application'

final String sdkRepoKey = project.findProperty('aferoSDKConsumeRepoKey') ?: 'afero-java-sdk'
final String sdkVersion = project.findProperty('aferoSDKVersion') ?: '0.8.0'

repositories {
    maven {
        url "https://afero.jfrog.io/afero/${sdkRepoKey}"
        credentials {
            username project.aferoArtifactoryUserName
            password project.aferoArtifactoryPassword
        }
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion '26.0.1'
    defaultConfig {
        applicationId 'io.afero.aferolab'
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 1
        versionName '1.0'

        buildConfigField 'String', 'AFERO_CLIENT_ID', project.aferoClientId
        buildConfigField 'String', 'AFERO_CLIENT_SECRET', project.aferoClientSecret
        buildConfigField 'String', 'AFERO_SERVICE_URL', project.findProperty('aferoServiceUrl') ?: '"https://api.afero.io"'
        buildConfigField 'String', 'AFERO_SOFTHUB_SERVICE', project.findProperty('aferoSofthubService') ?: '"prod"'
    }

    buildTypes {
        debug {
            buildConfigField 'okhttp3.logging.HttpLoggingInterceptor.Level', 'HTTP_LOG_LEVEL', 'okhttp3.logging.HttpLoggingInterceptor.Level.BODY'
        }

        release {
            buildConfigField 'okhttp3.logging.HttpLoggingInterceptor.Level', 'HTTP_LOG_LEVEL', 'okhttp3.logging.HttpLoggingInterceptor.Level.BASIC'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            minifyEnabled false
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // https://github.com/aferodeveloper/AferoJavaSDK
    compile "io.afero.sdk:afero-sdk-core:${sdkVersion}"
    compile "io.afero.sdk:afero-sdk-client-retrofit2:${sdkVersion}"
    compile "io.afero.sdk:afero-sdk-android:${sdkVersion}"
    compile "io.afero.sdk:afero-sdk-softhub:${sdkVersion}"

    // https://github.com/square/retrofit
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-jackson:2.3.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.3.0'

    // https://github.com/square/okhttp
    compile 'com.squareup.okhttp3:okhttp:3.8.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.8.0'

    // https://github.com/ReactiveX/RxAndroid
    compile 'io.reactivex:rxandroid:1.2.1'

    // https://github.com/ReactiveX/RxJava
    compile 'io.reactivex:rxjava:1.3.0'

    // http://jakewharton.github.io/butterknife/
    compile 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'

    // https://github.com/dm77/barcodescanner
    compile 'me.dm7.barcodescanner:zxing:1.9.8'

    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'

    testCompile 'junit:junit:4.12'
    compile 'com.android.support:cardview-v7:25.3.1'
}
